buildscript {
    ext.safeExtGet = {prop, fallback ->
        rootProject.ext.has(prop) ? rootProject.ext.get(prop) : fallback
    }

    def kotlin_version = safeExtGet('kotlinVersion', '1.7.22')

    repositories {
        google()
        gradlePluginPortal()
    }
    dependencies {
        classpath("com.android.tools.build:gradle:7.2.1")
        classpath("org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version")
    }
}

def isNewArchitectureEnabled() {
    return project.hasProperty("newArchEnabled") && project.newArchEnabled == "true"
}

apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
if (isNewArchitectureEnabled()) {
    apply plugin: "com.facebook.react"
}

android {
    compileSdkVersion safeExtGet('compileSdkVersion', 33)

    namespace "com.reactnativelegal"

    defaultConfig {
        minSdkVersion safeExtGet('minSdkVersion', 21)
        targetSdkVersion safeExtGet('targetSdkVersion', 33)
        buildConfigField "boolean", "IS_NEW_ARCHITECTURE_ENABLED", isNewArchitectureEnabled().toString()
    }

    sourceSets {
        main {
            if (isNewArchitectureEnabled()) {
                java.srcDirs += ['src/newarch/java', "${project.buildDir}/generated/source/codegen/java"]
            } else {
                java.srcDirs += ['src/oldarch/java']
            }
        }
    }
}

repositories {
    maven {
        url "$projectDir/../node_modules/react-native/android"
    }
    mavenCentral()
    google()
}

apply from: "$projectDir/react-native-helpers.gradle"

dependencies {
    if (project.ext.shouldConsumeReactNativeFromMavenCentral()) {
        implementation "com.facebook.react:react-android" // Set by the React Native Gradle Plugin
    } else {
        implementation 'com.facebook.react:react-native:+' // From node_modules
    }

    def aboutLibrariesPluginVersion = "10.8.3"
    implementation("com.mikepenz:aboutlibraries-core-android:$aboutLibrariesPluginVersion")
    implementation("com.mikepenz:aboutlibraries:$aboutLibrariesPluginVersion") {
        exclude group: 'com.mikepenz', module: 'aboutlibraries-core'
    }

    def constraintLayoutVersion = "2.1.4"
    implementation("androidx.constraintlayout:constraintlayout:$constraintLayoutVersion")
}
